apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    def getVersionName = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    defaultConfig {
        applicationId "com.github.yeriomin.yalpstore"
        minSdkVersion 5
        targetSdkVersion 27
        versionCode 40
        versionName getVersionName()
        vectorDrawables.useSupportLibrary = true
        resConfigs "ar", "ast", "cs", "de", "el", "en", "es", "eu", "fr", "fr-rBE", "it", "ja", "kk", "ko", "nl", "pl", "pt", "pt-rBR", "ru", "sk", "uk", "zh"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "minApi"
    productFlavors {
        legacy {
            dimension "minApi"
        }
        contemporary {
            dimension "minApi"
            minSdkVersion 14
            versionCode (1000 + android.defaultConfig.versionCode)
            applicationIdSuffix "plus"
        }
    }
    lintOptions {
        warning 'MissingTranslation','GetLocales','VectorDrawableCompat'
    }
}

dependencies {
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    contemporaryImplementation 'com.android.support:design:27.1.1'
    contemporaryImplementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'commons-net:commons-net:3.5'
    implementation 'com.nothome:javaxdelta:2.0.1'
    debugImplementation 'com.github.yeriomin:play-store-api:master-SNAPSHOT'
    releaseImplementation 'com.github.yeriomin:play-store-api:0.40'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201608240809'
}
