apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    def getVersionName = { ->
        def stdout = new ByteArrayOutputStream()
        try {
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = stdout
            }
        } catch (Throwable e) {
            println "Could not get git commit info for the version name: " + e.message
            return "0." + (android.defaultConfig.versionCode > 1000 ? (android.defaultConfig.versionCode - 1000) : android.defaultConfig.versionCode) + "-nogit"
        }
        return stdout.toString().trim()
    }
    defaultConfig {
        applicationId "com.github.yeriomin.yalpstore"
        minSdkVersion 5
        targetSdkVersion 27
        versionCode 43
        versionName getVersionName()
        vectorDrawables.useSupportLibrary = true
        resConfigs "ar", "ast", "bg", "cs", "de", "el", "en", "es", "eu", "fr", "fr-rBE", "in", "it", "ja", "kk", "ko", "nl", "pl", "pt", "pt-rBR", "ru", "sk", "uk", "zh", "zh-rTW"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "minApi"
    productFlavors {
        legacy {
            dimension "minApi"
            versionName getVersionName() + "-legacy"
        }
        contemporary {
            dimension "minApi"
            versionName getVersionName() + "-contemporary"
            minSdkVersion 14
            versionCode (1000 + android.defaultConfig.versionCode)
        }
    }
    lintOptions {
        warning 'MissingTranslation','GetLocales','VectorDrawableCompat'
    }
}

dependencies {
    contemporaryImplementation 'com.android.support:design:27.1.1'
    contemporaryImplementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-beta1'
    implementation 'commons-net:commons-net:3.5'
    implementation 'com.nothome:javaxdelta:2.0.1'
    debugImplementation ('com.github.yeriomin:play-store-api:master-SNAPSHOT') { changing = true }
    releaseImplementation 'com.github.yeriomin:play-store-api:0.44.1'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201608240809'
}
