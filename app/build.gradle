apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    def getVersionName = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    defaultConfig {
        applicationId "com.github.yeriomin.yalpstore"
        minSdkVersion 5
        targetSdkVersion 26
        versionCode 28
        versionName getVersionName()
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warning 'MissingTranslation'
        disable 'GoogleAppIndexingWarning','GoogleAppIndexingApiWarning','TrulyRandom','BadHostnameVerifier','IconMissingDensityFolder','IconLocation','UnusedAttribute'
    }

    def otaDir = "${project.rootDir}/yalp-store-ota-zip"
    def apkDir = "${project.buildDir}/outputs/apk"
    def otaZip = "yalp-store-ota-${android.defaultConfig.versionCode}.zip"
    def apkFilename = "YalpStore.apk"
    task deleteOldOtaZip(type: Delete) {
        delete "${project.buildDir}/${otaZip}"
        followSymlinks = true
    }
    task downloadYalpStoreApk(dependsOn: build) {
        doLast {
            def f = new File("${project.buildDir}/${apkFilename}")
            def url = "https://f-droid.org/repo/" + android.defaultConfig.applicationId + "_" + android.defaultConfig.versionCode + ".apk"
            if (!f.exists()) {
                println "Downloading ${url}"
                new URL(url).withInputStream { i -> f.withOutputStream { it << i } }
            }
        }
    }
    task generateOtaZipStable (dependsOn: [downloadYalpStoreApk, deleteOldOtaZip], type: Zip) {
        from(otaDir) {
            include('**')
        }
        from(project.buildDir) {
            include(apkFilename)
        }
        destinationDir project.buildDir
        archiveName otaZip
    }
    task copyYalpStoreApk(dependsOn: build, type: Copy) {
        def fileToCopy = file("${apkDir}/app-debug.apk")
        from fileToCopy
        into apkDir
        rename { String fileName ->
            fileName.replace(fileToCopy.getName(), apkFilename)
        }
    }
    task generateOtaZip (dependsOn: [build, deleteOldOtaZip, copyYalpStoreApk], type: Zip) {
        from(otaDir) {
            include('**')
        }
        from(apkDir) {
            include(apkFilename)
        }
        destinationDir project.buildDir
        archiveName otaZip
    }
    project.tasks.build.finalizedBy(generateOtaZip)
}

dependencies {
    compile 'commons-net:commons-net:3.5'
    compile 'com.nothome:javaxdelta:2.0.1'
    debugCompile 'com.github.yeriomin:play-store-api:master-SNAPSHOT'
    releaseCompile 'com.github.yeriomin:play-store-api:0.27'
    compile 'eu.chainfire:libsuperuser:1.0.0.201608240809'
}
